//file:noinspection GroovyAssignabilityCheck

plugins {
  id("java")
  id("com.github.johnrengelman.shadow").version("7.1.2")
}

dependencies {
  implementation(project(":api"))

  implementation("net.elytrium:fastprepare:$fastPrepareVersion")
  compileOnly("com.velocitypowered:velocity-api:$velocityVersion")
  annotationProcessor("com.velocitypowered:velocity-api:$velocityVersion")
  compileOnly("com.velocitypowered:velocity-proxy:$velocityVersion") // From Elytrium Repo.
  compileOnly("com.velocitypowered:velocity-native:$velocityVersion")

  // Needs for some velocity methods.
  compileOnly("io.netty:netty-codec:$nettyVersion")
  compileOnly("io.netty:netty-handler:$nettyVersion")
  compileOnly("it.unimi.dsi:fastutil-core:$fastutilVersion")

  implementation("org.bstats:bstats-velocity:$bstatsVersion")

  compileOnly("com.github.spotbugs:spotbugs-annotations:$spotbugsVersion")
}

shadowJar {
  getArchiveClassifier().set(this.getVersion().contains("-") ? getCurrentShortRevision() : "release")
  setArchiveFileName("limboapi-${version}-${classifier}.${extension}")

  exclude("META-INF/versions/**")
  exclude("net/kyori/**")

  relocate("org.bstats", "net.elytrium.limboapi.thirdparty.org.bstats")
  relocate("net.elytrium.fastprepare", "net.elytrium.limboapi.thirdparty.fastprepare")
  relocate("net.elytrium.commons", "net.elytrium.limboapi.thirdparty.commons")
}

license {
  matching(includes: ["**/mcprotocollib/**"]) {
    setHeader(getRootProject().file("HEADER_MCPROTOCOLLIB.txt"))
  }
  matching(includes: ["**/LoginListener.java", "**/KickListener.java", "**/LoginTasksQueue.java", "**/MinecraftLimitedCompressDecoder.java"]) {
    setHeader(getRootProject().file("HEADER_MIXED.txt"))
  }

  setHeader(getRootProject().file("HEADER.txt"))
}

task finalize {
  doLast {
    if (this.getVersion().contains("-")) {
      file("build/libs/${this.getName()}-${this.getVersion()}.jar").delete()
    }
  }
}

assemble.dependsOn(shadowJar)
build.finalizedBy(finalize)
